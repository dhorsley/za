#!/usr/bin/za

# library html ref gen

interpol(false)

style=`
    .0 {color:#000;}
    .1 {color:#00a;}
    .2 {color:#a00;}
    .3 {color:#a0a;}
    .4 {color:#0a0;}
    .5 {color:#0aa;}
    .6 {color:#aa0;}
    .7 {color:#aaa;}
    .ioff       {font-style:normal;}
    .ion        {font-style:italic;}
    .boff       {font-weight:normal;}
    .bon        {font-weight:bold;}
    .ttab       {display:table}
    .ttab > div {display:table-cell}
    .ttableft   {width:90%;padding-left:16px;}
    .ttabright  {width:9%;}
    .hdbg       {width:100%;background-color:#07b;}
    .ilink      {width:100%;font-size:2.0rem; }
    a:link      {color:blue;background-color:transparent;text-decoration:none;}
    a:visited   {color:blue;background-color:transparent;text-decoration:none;}
    a:hover     {color:blue;background-color:transparent;text-decoration:none;}
    a:active    {color:blue;background-color:transparent;text-decoration:none;}
`
interpol(true)

hc["\[#0]"] = `<span class='.0'>`
hc["\[#1]"] = `<span class='.1'>`
hc["\[#2]"] = `<span class='.2'>`
hc["\[#3]"] = `<span class='.3'>`
hc["\[#4]"] = `<span class='.4'>`
hc["\[#5]"] = `<span class='.5'>`
hc["\[#6]"] = `<span class='.6'>`
hc["\[#7]"] = `<span class='.7'>`

define clean(s)

    s=replace(s,`\[#i0]`,    `<span class='ioff'>`)
    s=replace(s,`\[#i1]`,    `<span class='ion'>`)
    s=replace(s,`\[#bold]`,  `<span class='boff'>`)
    s=replace(s,`\[#boff]`,  `<span class='bon'>`)
    s=replace(s,"`\n",       `<br>`)

    foreach c in getglob("hc")
        s=replace(s,key_c,c)
    endfor

    return s

enddef


# main

docpath="funpages"

fi=func_inputs()
fo=func_outputs()
fd=func_descriptions()

# get all categories

foreach c in func_categories()

    header=wh1()+"\n"
    header=header+wh2("{key_c} functions") 

    body=""
    pageindex=""

    foreach i in c

        pageindex=pageindex+wli(wa(i,`href="#{i}"`))+"\n"

        in=clean(fi[i])
        out=clean(fo[i])
        desc=clean(html_escape(fd[i]))

        if out!=""
            fnline=`{out} = {i}({in})` 
        else
            fnline=`{i}({in})`
        endif

        #    wdiv( "&nbsp;&nbsp;&nbsp;" ) +
        body=body+wdiv(
            wdiv( wa("",`id="{i}"`)+wh3(fnline) , `class="ttableft"` ) + 
            wdiv( wa("top",`href="#" onclick="document.body.scrollTop=0;document.documentElement.scrollTop=0;event.preventDefault()"`,`class="ttabright"`)),
          `class="ttab hdbg"`
        )
        body=body+wp("{desc}")
        body=body+"<br>\n"

    endfor


    meta=`
      <meta charset="UTF-8">
      <meta name="viewport" content="width=device-width, initial-scale=1, minimum-scale=1">
      <meta name="description" content="Za library reference - {key_c} functions">
      <title>{key_c} functions</title>
    `

    page=wpage(
        whead(format(meta)+"<style>\n"+style+"\n</style>\n")+
        wbody(
            header+"\n"+
            wa("",`id="top"`)+"\n"+
            wul(pageindex)+"\n"+
            body+"\n"
        ),
      `lang="en"`
    )

    println "{docpath}/{key_c}.html"
    write_file(docpath+"/"+key_c+".html",page)

endfor

# generate index page

indices=""

foreach c in func_categories()
    indices=indices+wli(
        wa(key_c,`href="{key_c}.html"`)+"\n",
        `class="ilink"`
    )
endfor

indexpage=wpage(
        whead(format(meta)+"<style>\n"+style+"\n</style>\n")+
        wbody(
            wdiv( wh1("Za Function Reference") )+
            wdiv( wh2("Categories:") )+
            wdiv( wul(indices) , `class="ttab"` )
        )
)

write_file(docpath+"/index.html",indexpage)

exit 0


