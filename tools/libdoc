#!/usr/bin/za

# library html ref gen

interpol(false)

style=`
    .f0 {color:#000;}
    .f1 {color:#00a;}
    .f2 {color:#a00;}
    .f3 {color:#a0a;}
    .f4 {color:#0a0;}
    .f5 {color:#0aa;}
    .f6 {color:#cd0;}
    .f7 {color:#bef;}
    .ioff       {font-style:normal;}
    .ion        {font-style:italic;}
    .boff       {font-weight:normal;}
    .bon        {font-weight:bold;}
    .ttab       {display:table}
    .ttab > div {display:table-cell}
    .ttableft   {width:90%;padding-left:16px;}
    .ttabright  {width:9%;}
    .hdbg       {width:100%;background-color:#07b;}
    .ilink      {width:100%;font-size:2.0rem; }
    a:link      {color:blue;background-color:transparent;text-decoration:none;}
    a:visited   {color:blue;background-color:transparent;text-decoration:none;}
    a:hover     {color:blue;background-color:transparent;text-decoration:none;}
    a:active    {color:blue;background-color:transparent;text-decoration:none;}
    .footer     {background-color:#259;color:#8cc;width:99%;padding:4px;text-align:right;font-size:0.85rem;}
`
interpol(true)

hc[`\[#0]`] = `<span class='f0'>`
hc[`\[#1]`] = `<span class='f1'>`
hc[`\[#2]`] = `<span class='f2'>`
hc[`\[#3]`] = `<span class='f3'>`
hc[`\[#4]`] = `<span class='f4'>`
hc[`\[#5]`] = `<span class='f5'>`
hc[`\[#6]`] = `<span class='f6'>`
hc[`\[#7]`] = `<span class='f7'>`
hc[`\[#-]`] = `</span>`
hc[`\[##]`] = `</span>`

define clean(s)

    s=replace(s,`\[#i0]`,    `</span>`)
    s=replace(s,`\[#i1]`,    `<span class='ion'>`)
    s=replace(s,`\[#bold]`,  `<span class='boff'>`)
    s=replace(s,`\[#boff]`,  `</span>`)
    s=replace(s,`\n`,        `<br><br>`)

    foreach c in getglob("hc")
        s=replace(s,key_c,c)
    endfor

    return s

enddef


# main

docpath="funpages"
egpages="{docpath}/examples"

zero totfunc

fi=func_inputs()
fo=func_outputs()
fd=func_descriptions()

# get all categories

foreach c in func_categories()

    header=wh1()+"\n"
    header=header+wh2("{key_c} functions") 

    body=""
    pageindex=""

    sfuncs=[]
    foreach i in c
        sfuncs=append(sfuncs,i)
    endfor
    sort(sfuncs)

    foreach i in sfuncs

        inc totfunc

        pageindex=pageindex+wli(wa(i,`href="#{i}"`))+"\n"

        in=clean(fi[i])
        out=clean(fo[i])
        desc=clean(html_escape(fd[i]))

        if out!=""
            fnline=clean(`[#7]{out} = {i}({in})[#-]`)
        else
            fnline=clean(`[#6]{i}({in})[#-]`)
        endif

        body=body+wdiv(
            wdiv( wa("",`id="{i}"`)+wh3(fnline) , `class="ttableft"` ) + 
            wdiv( wa("top",`href="#" onclick="document.body.scrollTop=0;document.documentElement.scrollTop=0;event.preventDefault()"`,`class="ttabright"`)),
          `class="ttab hdbg"`
        )
        body=body+wp(`{desc}`)

        if is_file("{egpages}/{i}")
            body=body+wp("<i>Example:</i><br><pre>"+html_escape(read_file("{egpages}/{i}"))+"</pre>")
        endif

        body=body+"<br>\n"

    endfor

    footer="<br>"+wdiv(`<br><i>page generated by libdoc on {date_human()}</i><br><br>`,`class="footer"`)

    meta=`
      <meta charset="UTF-8">
      <meta name="viewport" content="width=device-width, initial-scale=1, minimum-scale=1">
      <meta name="description" content="Za library reference - {key_c} functions">
      <title>{key_c} functions</title>
    `

    mainindex=wdiv(
        wa("Return to index",`href="./index.html"`)
    )

    page=wpage(
        whead(format(meta)+"<style>\n"+style+"\n</style>\n")+
        wbody(
            mainindex+"\n"+
            header+"\n"+
            wa("",`id="top"`)+"\n"+
            wul(pageindex)+"\n"+
            body+"\n"
        )+
        footer,
      `lang="en"`
    )

    println "{docpath}/{key_c}.html"
    write_file(docpath+"/"+key_c+".html",page)

endfor

# generate index page

indices=""

scat=[]
foreach c in func_categories()
    scat=append(scat,key_c)
endfor
sort(scat)

foreach c in scat
    indices=indices+wli(
        wa(c,`href="{c}.html"`)+"\n",
        `class="ilink"`
    )
endfor

    meta=`
      <meta charset="UTF-8">
      <meta name="viewport" content="width=device-width, initial-scale=1, minimum-scale=1">
      <meta name="description" content="Za library reference - {key_c} functions">
      <title>Za functions</title>
    `

indexpage=wpage(
        whead(format(meta)+"<style>\n"+style+"\n</style>\n")+
        wbody(
            wdiv( wh1("Za Function Reference") )+
            wdiv( wh2("Categories:") )+
            wdiv( wul(indices) , `class="ttab"` )
        )
)

write_file(docpath+"/index.html",indexpage)

println "Total functions : {totfunc}"

exit 0


