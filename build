#!/usr/bin/za

module "mod_build/defines"

input target optarg 1
input do_upx optarg 2

tgt="libc"
on target!="" do tgt=target

upx_enable=false
on do_upx=="upx" do upx_enable=true

#
# main
#

GCLIMIT = "GOGC=250 GOMAXPROCS=4"
GCFLAGS = "-gcflags=-B"

bvers =  "1.0.11"
bdate =| date "+%d-%m-%Y %H:%M"


upxfound=| which upx | wc -l
upxfound=int(trim(upxfound,0))

strip_opts="-s -w"

if !start(os(),"freebsd")
    when lower(tgt)
    is "alpine"
        if buildMusl(bvers,bdate,strip_opts,upx_enable)!=0
            print "Failed to build alpine version.\n"
            exit 1
        endif
    is "libc"
        if buildLibc(bvers,bdate)!=0
            print "Failed to build libc version.\n"
            exit 1
        endif
    is "win"
        if buildWin(bvers,bdate,strip_opts) != 0
            print "Failed to build windows version.\n"
            exit 1
        endif
    endwhen
else
    # r=buildFreeBSD(bvers,bdate)
    # println "R::",r
    # if r!=0
    if buildFreeBSD(bvers,bdate) != 0
        print "Failed to build bsd version.\n"
        exit 1
    endif
endif

if !start(os(),"freebsd")
    if upxfound==1 && is_file("za")
        if tgt!="win"
            | strip {strip_opts} za
            on upx_enable do | upx za
        endif
    else
        on is_file("za") do print "[#6]Note: Please consider installing the upx package for compressing the binary.[#-]\n"
    endif
endif

when lower(tgt)
is "libc"
    print "[#4]Installing to /usr/bin/[#-]\n\n"
    | sudo cp -f za /usr/bin
    | sudo chmod 755 /usr/bin/za
endwhen



