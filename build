#!/usr/bin/za

module "mod_build/defines" as defs
use +defs

# logging on "build.log"

var strip_enable,upx_enable bool

tgt="default"
ipath="/usr/bin"

gobin="go"

for a = 1 to argc()
    input do_what optarg a
    case $lc do_what
        is "help"
            exit 0,`\n./build [libc|win|arch|alpine] [upx] [strip] [-path "install_path"]\n`
        is "upx"    ; upx_enable=true
        is "strip"  ; strip_enable=true
        is "arch"   ; tgt="arch"
        is "alpine" ; tgt="alpine"
        is "bsd"    ; tgt="bsd"
        is "libc"   ; tgt="libc"
        is "win"    ; tgt="win"
        is "beta"   ; gobin="gobe"
        is "-path"
            ipath=argv()[a]
            a++
            println "install path changed to {ipath}"
    endcase
endfor


#
# main
#

GCLIMIT = "madvdontneed=0 GOGC=1600"
GCFLAGS = "-buildvcs=false -gcflags='-B -l=4'"

GCCGOFLAGS=""
doc " uncomment line below when testing with GCC-GO compiler:
    GCCGOFLAGS = `-compiler=gccgo -gccgoflags='-O3 -march=native -ffast-math -lpthread'`
"

bvers = $in "VERSION"
bdate =< date "+%d-%m-%Y %H:%M"

strStrip    = strip_enable ? "-stripped" : ""
stripFlags  = strip_enable ? "-s -w"     : ""

upxfound= ${which upx | wc -l} . trim(0) . as_bool

if ! os() ~ "^freebsd"
    case tgt
    is "default"
        on buildDefault(bvers,bdate)!=0 do exit e.build,"Failed to build default version."
    is "arch"
        on buildArch(bvers,bdate)!=0 do exit e.build,"Failed to build arch version."
    has tgt ~i "^(alpine)$"
        on buildMusl(bvers,bdate)!=0 do exit e.build,"Failed to build musl version."
    is "libc"
        on buildLibc(bvers,bdate)!=0 do exit e.build,"Failed to build libc version."
    is "bsd"
    on buildFreeBSD(bvers,bdate) != 0 do exit e.build,"Failed to build bsd version."
    is "win"
        on strip_enable do "Windows build does not strip!".warning
        on buildWin(bvers,bdate) != 0 do exit e.build,"Failed to build windows version."
    or
        exit e.notfound,"Target type unknown."
    endcase
else
    on buildFreeBSD(bvers,bdate) != 0 do exit e.build,"Failed to build bsd version."
endif


if is_file("za")
    if not os() ~ "^freebsd"
        if upxfound
            if tgt!="win"
                if strip_enable and not tgt ~i "^(alpine)$"
                    | strip -s -w za
                    on last()!=0 do "Note: Could not strip binary.".warning
                endif
                if upx_enable
                    | upx za
                    on last()!=0 do "Note: Could not compact binary.".warning
                endif
            endif
        else
            "Note: Please consider installing the upx package for compressing the binary.".warning
        endif
    else
        # don't strip/upx freebsd version
    endif
endif


case tgt
contains "^(default|alpine|arch|libc)$"
    "Installing {tgt} build to {ipath}".progress
    | sudo cp -f za {ipath}
    on last()!=0 do "sudo copy failed.".warning
    | sudo chmod 755 {ipath}/za
    on last()!=0 do "sudo chmod failed.".warning
endcase


