#!/usr/bin/za

module "mod_build/defines"

tgt="alpine"
strip_enable=false
upx_enable=false
ipath="/usr/bin"

for e = 1 to argc()
    input do_what optarg e
    when $lc do_what
        is "help"
            exit 0,`\n./build [libc|win|alpine] [upx] [strip] [-path "install_path"]\n`
        is "upx"    ; upx_enable=true
        is "strip"  ; strip_enable=true
        is "alpine" ; tgt="alpine"
        is "libc"   ; tgt="libc"
        is "win"    ; tgt="win"
        is "-path"
            ipath=argv()[e]
            e++
            println "install path changed to {ipath}"
    endwhen
endfor


#
# main
#

GCLIMIT = "GOGC=80 GOMAXPROCS=4"
GCFLAGS = "-gcflags='-B -l=4'"

GCCGOFLAGS=""
doc ` uncomment line below for testing with GCC-GO compiler:
    GCCGOFLAGS = `-compiler=gccgo -gccgoflags='-O3 -march=native -ffast-math -lpthread'`
`

bvers =  "1.1.0"
bdate =< date "+%d-%m-%Y %H:%M"

strStrip    = ?? strip_enable "-stripped" : ""
stripFlags  = ?? strip_enable "-s -w" : ""

upxfound=| which upx | wc -l
upxfound=trim(upxfound.out,0).int

if ! os() ~ "^freebsd"
    when tgt
    has tgt ~i "^(alpine|aws)$"
        if buildMusl(bvers,bdate)!=0
            exit e.build,"Failed to build musl version."
        endif
    is "libc"
        if buildLibc(bvers,bdate)!=0
            exit e.build,"Failed to build libc version."
        endif
    is "win"
        on strip_enable do "Windows build does not strip!".warning
        if buildWin(bvers,bdate) != 0
            exit e.build,"Failed to build windows version."
        endif
    or
        exit e.notfound,"Target type unknown."
    endwhen
else
    if buildFreeBSD(bvers,bdate) != 0
        exit e.build,"Failed to build bsd version."
    endif
endif


if is_file("za")
    if not os() ~ "^freebsd"
        if upxfound==1
            if tgt!="win"
                on strip_enable and not tgt ~i "^(alpine|aws)$" do | strip -s -w za
                on upx_enable                                   do | upx za
            endif
        else
            println "[#6]Note: Please consider installing the upx package for compressing the binary.[#-]"
        endif
    else
        # don't strip/upx freebsd version
    endif
endif


when tgt
contains "^(alpine|libc)$"
    println "[#4]Installing {tgt} build to {ipath}[#-]\n"
    | sudo cp -f za {ipath}
    | sudo chmod 755 {ipath}/za
endwhen


