#!/usr/bin/za

module "mod_build/defines"

tgt="libc"
strip_enable=false
upx_enable=false
input tgt optarg 1
input do_upx optarg 2
on do_upx=="upx" do upx_enable=true

#
# main
#

GCLIMIT = "GOGC=250 GOMAXPROCS=4"
GCFLAGS = "-gcflags='-B -l=4'"

bvers =  "1.0.13"
bdate =| date "+%d-%m-%Y %H:%M"


upxfound=| which upx | wc -l
upxfound=trim(upxfound.out,0).int

strip_opts="-s -w"

if ! os() ~ "^freebsd"
    when lower(tgt)
    is "alpine"
        if buildMusl(bvers,bdate.out,strip_opts,upx_enable)!=0
            println "Failed to build alpine version."
            exit 1
        endif
    is "libc"
        if buildLibc(bvers,bdate.out)!=0
            println "Failed to build libc version."
            exit 1
        endif
    is "win"
        if buildWin(bvers,bdate.out,strip_opts) != 0
            println "Failed to build windows version."
            exit 1
        endif
    endwhen
else
    if buildFreeBSD(bvers,bdate.out) != 0
        println "Failed to build bsd version."
        exit 1
    endif
endif

if ! os() ~ "^freebsd"
    if upxfound==1 && is_file("za")
        if tgt!="win"
            on strip_enable  do | strip {strip_opts} za
            on upx_enable    do | upx za
        endif
    else
        on is_file("za") do println "[#6]Note: Please consider installing the upx package for compressing the binary.[#-]"
    endif
endif

when lower(tgt)
is "libc"
    println "[#4]Installing to /usr/bin/[#-]\n"
    | sudo cp -f za /usr/bin
    | sudo chmod 755 /usr/bin/za
endwhen



