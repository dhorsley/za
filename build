#!/usr/bin/za

module "mod_build/defines"

tgt="libc"
strip_enable=false
upx_enable=false
input tgt optarg 1
input do_upx optarg 2
on do_upx=="upx" do upx_enable=true

#
# main
#

GCLIMIT = "GOGC=250 GOMAXPROCS=4"
GCFLAGS = "-gcflags='-B -l=4'"

bvers =  "1.0.15"
bdate =| date "+%d-%m-%Y %H:%M"

upxfound=| which upx | wc -l
upxfound=trim(upxfound.out,0).int

if ! os() ~ "^freebsd"
    when lower(tgt)
    has tgt ~i "^(alpine|aws)$"
        if buildMusl(bvers,bdate.out)!=0
            exit e.build,"Failed to build no ui version."
        endif
    is "libc"
        if buildLibc(bvers,bdate.out)!=0
            exit e.build,"Failed to build libc version."
        endif
    is "win"
        if buildWin(bvers,bdate.out) != 0
            exit e.build,"Failed to build windows version."
        endif
    or
        exit e.notfound,"Target type unknown."
    endwhen
else
    if buildFreeBSD(bvers,bdate.out) != 0
        exit e.build,"Failed to build bsd version."
    endif
endif

if ! os() ~ "^freebsd"
    if upxfound==1 && is_file("za")
        if tgt!="win"
            on ! tgt ~i "^(alpine|aws)$" and strip_enable do | strip -s -w za
            on upx_enable do | upx za
        endif
    else
        on is_file("za") do println "[#6]Note: Please consider installing the upx package for compressing the binary.[#-]"
    endif
endif

when lower(tgt)
contains "^(alpine|libc)$"
    println "[#4]Installing {tgt} build to /usr/bin/[#-]\n"
    | sudo cp -f za /usr/bin
    | sudo chmod 755 /usr/bin/za
endwhen


