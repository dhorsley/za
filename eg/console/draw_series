#!/usr/bin/za

define vbar(row,col,vsize,pc,colour)
    b1000 = "█"
    b0750 = "▆"
    b0500 = "▄"
    b0250 = "▂"
    b0125 = "▁"
    us = vsize / 100f   # 1% width of total
    d  = pc*us          # height of input percent
    r  = d-d.int        # remainder
    for e=0 to vsize-1
        at row-e,col
        c=" "
        on e<d.int do c=b1000
        if e>=d.int and e<int(d+1)
            on r>=0.125*us do c=b0125
            on r>=0.25*us do c=b0250
            on r>=0.50*us do c=b0500
            on r>=0.75*us do c=b0750
        endif
        print "[#{colour}]{c}[#-]"
    endfor
end

define chart(row,col,max_height,series)
    oc=-1
    for e = 0 to series.len-1
        while
            c=rand(7)
            on c!=oc do break
        endwhile
        oc=c
        vbar(row,col+e,max_height,series[e],c)
        on e%10==0 do at row+1,col+e,e
    endfor
end

cls
cursoroff()
seed(epoch_nano_time())

var series [rand(80)] int
for e = 0 to series.len-1
    series[e]=rand(100)
endfor

chart(30,10,rand(25),series)
at 32,1
cursoron()


