#!/usr/bin/za

hs=20
rep=1
input rep optarg 1

define bar(row,col,hsize,pc,colour)
    # colour=colour #bodge
    part_3q="▊"
    part_2q="▌"
    part_1q="▎"
    us = hsize / 100f   # 1% width of total
    d  = pc*us          # width of input percent
    r  = d-d.as_int     # remainder
    for e=0 to hsize-1
        at row,col+e
        c=" "
        on e<d.as_int do c="█"
        if e>=d.as_int and e<as_int(d+1)
            on r>=0.25*us do c=part_1q
            on r>=0.50*us do c=part_2q
            on r>=0.75*us do c=part_3q
        endif
        print "[#{colour}]{c}[#-]"
    endfor
end

define spin(sp)
    var spin [4]string
    spin[0]="◐"
    spin[1]="◓"
    spin[2]="◑"
    spin[3]="◒"
return "[#{=1+sp}]"+spin[sp]+"[#-]"
end

cursoroff()
seed(epoch_time())

for i=0 to rep.as_int-1
    row=get_row()
    c=rand(7)
    sp=0
    for e = 0 to 100
        at row,1,"Progress : "
        bar(row,12,hs,e,c)
        msg=" {e} % "
        spinmsg=" "+spin(sp)
        on e==100 do spinmsg=" "
        print spinmsg,msg
        on e%5==0 do sp=++sp%4
        pause 2
    endfor
    println
    hs=8+rand(16)
endfor

cursoron()
print


