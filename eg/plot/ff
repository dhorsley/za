#!/usr/bin/za

#
# example flood fill inside a text buffer shape
#

cls

var screen [term_h()-1] string
# screen[0] will be unused
for i=0 to 30
    screen[i]=80*" "
endfor

# draw enclosing shape
screen[02]="******     *****   *****    "
screen[03]="*    *     *   *   *   *    "
screen[04]="***  *    **  **   *   *    "
screen[05]="  *  *    *   *    *   *    "
screen[06]="  *  *****    *****     *** "
screen[07]="  *             *         * "
screen[08]=" **             *         * "
screen[09]=" *             **         * "
screen[10]=" *                  ******* "
screen[11]=" *                  *       "
screen[12]=" *                  *       "
screen[13]=" *                  ******  "
screen[14]=" *             *          * "
screen[15]=" *             *          * "
screen[16]=" *             *          * "
screen[17]=" *             *          * "
screen[18]=" ************************** "

define show()
    for l=1 to 30
        at l,1; print screen[l]
    endfor
end

define fill(x,y,b,c)
    on x<1 or x>screen.len-1 do return
    on y<1 or y>term_w()-1 do return
    above=screen[x-1]
    line=screen[x]
    below=screen[x+1]
    on line[y]  ==b do setglob screen[x]=line[:y]+c+line[y+1:]
    on line[y-1]==b do fill(x,y-1,b,c)
    on above[y] ==b do fill(x-1,y,b,c)
    on line[y+1]==b do fill(x,y+1,b,c)
    on below[y] ==b do fill(x+1,y,b,c)
end

cursoroff()
show()
at 35,1; print "press a key to fill box!"
keypress()
at 38,1; print "press <space> to quit!"

ob=" "
b="-"
quit=false

async hmap fill(13,13,ob,b)

while !quit
    when keypress(10)
    is 32
        quit=true
    is 13
        ob=b
        while
            b=char(32+rand(63))
            on b!="*" do break
        endwhile
        async hmap fill(13,13,ob,b)
    endwhen
    show()
    # small pause here - concurrency issues with fast spawning asyncs
    pause 5
endwhile

at term_h(),1
cursoron()




