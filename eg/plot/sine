#!/usr/bin/za

# about as similar to processing.js example as you can do with text chars

fps=20
input fps optarg 1


define calcWave(dx,amplitude)
    setglob theta = theta + 0.2
    x=theta
    yvalues=[]
    for i = 0 to width-1
        yvalues = append(yvalues,sin(x)*amplitude)
        x=x+dx
    endfor 
    return yvalues
end

define renderWave(yv,height,glyph)
    on len(yv)==0 do return
    on glyph==nil do glyph="+"
    for x = 0 to len(yv)-1
        y=int(height/2)+int(yv[x])
        at y,x+1
        print glyph
    endfor
end

define async_waitkey()
    keypress()
    setglob quit=true
end

#
# MAIN
#

cls

theta = 45.0
period = 120.0

height=term_h()
amplitude = int(height/2)
width=term_w()
yvalues=[]

dx = (2.0*pi() / period)


# key loop
quit=false
async retmap async_waitkey()


# display loop

cursoroff()

fps_pause=1000/fps

while !quit
    lastyary=yvalues
    yvalues=calcWave(dx,amplitude)

    renderWave(lastyary,height," ")
    renderWave(yvalues,height)
    at 1,1; println "theta -> ",theta, "  "
    pause fps_pause
endwhile

at term_h()-1,0 
cursoron()


