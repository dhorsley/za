#!/usr/bin/za

doc `
    bssh()
    call multiple hosts (with the current user) with ssh
    to batch execute a cli command.
    all commands sent asynchronously, with final results
    collected in the return value.
`

ValidIpAddressRegex= "^(([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5])\.){3}([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5])$"
ValidHostnameRegex = "^(([a-zA-Z]|[a-zA-Z][a-zA-Z0-9\-]*[a-zA-Z0-9])\.)*([A-Za-z]|[A-Za-z][A-Za-z0-9\-]*[A-Za-z0-9])$"
ssh_options = "-q"

def is_valid_host(h)
    on not h is string do return false
    on h ~ ValidIpAddressRegex do return true
    on h ~ ValidHostnameRegex do return true
    return false
end

def assh(host,cmd)
    r=|ssh {=ssh_options} {host} '{cmd}'
    return r
end

def bssh(hosts,cmd)
    foreach h in hosts
        break if not h.is_valid_host
        async rlist assh(h,cmd) h
    endfor

    res=await(ref rlist,true)
    return res
end


# MAIN

# example hosts to connect to:
host_list=["127.0.0.1","localhost","127.0.0.2"]

# call and examine results
foreach r in bssh(host_list,"w")
    println "[#4]{key_r}\n[#6]{=r.okay} : {=r.code}\n[#-]{=r.out}\n"
endfor


