#!/usr/bin/za

error_extend(true)

seed(-1)

trap("int","cleanup()")

def cleanup()
    tui_screen(0)
    cursoron()
    exit
end

struct member
    forename    string
    surname     string
    age         int
    town        string
endstruct

var members[] any
append_to(ref members, member(.forename "Fred", .surname "Flintstone", .age 30, .town "Bedrock" ) )
append_to(ref members, member(.forename "Bugs", .surname "Bunny", .age 80, .town "Albuquerque" ) )
append_to(ref members, member(.forename "Stimpson J", .surname "Cat", .age 3, .town "Los Angeles" ) )

# switch to alt screen
tui_screen(1)

# Create default style with map literal
s = map(.fill true, .bg "6", .fg "0", .hi_bg "6", .hi_fg "4", .border map(.bg "6", .fg "0"))
rad=nil
quit=false

while not quit

    # menu with map literal
    cursoroff()
    menu_config = map(.row 2, .col 2, .title "test options menu", .height 12, .width 32, 
                      .options [
                        "input box", "radio buttons", "progress bar", 
                        "text box", "pager", "templates", "quit"
                        ], 
                      .prompt "Please select an option:", .cursor "[#b1][#6]*[##][#-]", .index 0, .border true)
    
    menu = tui_menu(menu_config, s)

    break if menu.Cancel

    case menu.Result
    is 1
        # input box with map literal
        input_config = map(.row term_h()-4, .col 10, .content "blah", .prompt "[#b1][#6]type away... ", 
                          .title "test input box", .border true, .height 1, .width 80)
        input_style = map(.fill true, .bg "1", .fg "6", .border map(.bg "1", .fg "6"))
        i = tui_input(input_config, input_style)
        ires = i.Result
    is 2
        # radio buttons with map literal
        radio_config = map(.row 2, .col 36, .height 6, .width 64, .prompt "Please select : ", 
                          .options ["Red", "Green", "Blue", "White"], .vertical true, .multi true, 
                          .sep " ", .cursor " ", .selected rad,
                          .border true)
        radio_style = map(.fill true, .bg "0", .fg "5", .hi_bg "1", .hi_fg "6")
        rad = tui_radio(radio_config, radio_style)
        tui_clear(radio_config, radio_style)
    is 3
        # example progress bar with map literal
        progress_config = map(.row 20, .col 20, .width 80, .cursor "â–’", .border true)
        progress_style = map(.bg as_string(rand(5)), .fg as_string(5+rand(2)))
        for e=1 to 100
            progress_config["value"] = e.as_float/100
            pb = tui_progress(progress_config, progress_style)
            pause 10
        endfor
        # reset bar
        tui_progress_reset(progress_config)
    is 4
        # text box with map literal
        text_config = map(.row 2, .col 36, .height 15, .width 40, .title "text display", .border true,
                         .content "Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua. Ut enim ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut aliquip ex ea commodo consequat. Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum.")
        text_style = map(.wrap true, .bg "3", .fg "7", .fill true)
        tui_text(text_config, text_style)
        keypress(0)
        tui_clear(text_config, text_style)
    is 5
        # pager with map literal
        pager_config = map(.row 2, .col 36, .height 30, .width term_w()-40, .title "pager box", .border true)
        pager_style = map(.bg "2", .fg "7", .wrap true, .fill true, .border map(.bg "2", .fg "7"))
        pager_config["content"] = ""
        on is_file("CHANGELOG") do pager_config["content"] = $in "CHANGELOG"
        tui_pager(pager_config, pager_style)
        tui_clear(pager_config, pager_style)
    is 6
        # templates with map literal
        template_style = map(.wrap true, .bg "1", .fg "7", .fill true, .border map(.bg "1", .fg "7"))
        foreach rec in members
            template_config = map(.row 2+8*key_rec, .col 80, .height 4, .width 40, .border true, .fill true,
                                 .title " Record #{=key_rec+1} ", 
                                 .content "Name     : {.surname}, {.forename}\nAge      : {.age}\nLocation : {.town}", 
                                 .data rec)
            tui_template(template_config, template_style)
        endfor
        keypress()
        foreach rec in members
            clear_config = map(.row 2+8*key_rec, .col 80,.width 40, .height 4,.border true)
            tui_clear(clear_config, template_style)
        endfor
    is 7
        quit=true
    endcase

endwhile

# cleanup
tui_screen(0)
cursoron()
at term_h(),1


