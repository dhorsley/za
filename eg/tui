#!/usr/bin/za

seed(-1)

trap("int","cleanup()")

def cleanup()
    tui_screen(0)
    cursoron()
    exit
end

# set up a general tui and tui_style struct for re-use
t=tui_new()
s=tui_new_style()
t.row=5
t.col=10
t.height=15
t.width=100
t.title="test panel"

bg=0; fg=1
for ,bg==fg,
    bg,fg=[rand(7),rand(7)]
endfor
border=s.border
border["bg"]=bg.as_string
border["fg"]=fg.as_string
s.border=border
s.fill=true

# switch to alt screen
tui_screen(1)

# input box
i=tui_new()
i.row=term_h()-4
i.col=10
i.content="blah"
i.prompt="[#b1][#6]type away... "
i.title="test input box"
i.border=true
i.height,i.width=[1,80]
s.bg,s.fg=[as_string(rand(5)),as_string(5+rand(2))]
ires=i.tui_input(s)

# .. reset some colours
s.bg=bg.as_string
s.fg=fg.as_string
s.hi_fg="2"
s.hi_bg="7"

# menu
cursoroff()
t.tui_box(s)
menu=tui_new()
menu.row,menu.col,menu.action = [4,10,"menu"]
menu.options=[
    "cow","pig","cat","dog",
    "sheep","horse","monkey","spider","hamster",
]
menu.prompt ="Please select an option:"
menu.cursor="[#b1][#6]*[##][#-]"

result=menu.tui_menu(s)

# example progress bar
pb=tui_new()
pb.row,pb.col,pb.action = [2,20,"progress"]
pb.width=80
pb.cursor="â–’"
pb.border=true
s.bg,s.fg=[as_string(rand(5)),as_string(5+rand(2))]
for e=1 to 100
    pb.value=e.as_float/100
    pb.tui_progress(s)
    pause 25
endfor
keypress()

# reset bar
pb.value=0.0
pb.tui_progress(s)

# pager
s.bg,s.fg=["2","7"]
border["bg"]=s.bg
border["fg"]=s.fg
s.border=border
t.height,t.width,t.title=[30,118,"modal text box"]
t.tui_box(s)
t.content=$in "CHANGELOG"
t.tui_text_modal(s)
t.tui_clear

# cleanup
tui_screen(0)
cursoron()

at term_h(),1
println "input result -> ",ires
println "menu result  -> ",result
on result>0 do println "     option  -> ",menu.options[result-1]

