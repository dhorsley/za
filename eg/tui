#!/usr/bin/za

doc `

struct tui
    row     int
    col     int
    height  int
    width   int
    action  string
    options []
    cursor  string
    title   string
    prompt  string
    content string
    value   float
es

struct tui_style
    bg      string
    fg      string
    hi_bg   string
    hi_fg   string
    border  map[string]string
    fill    bool
    wrap    bool
es

`

cursoroff()
seed(-1)

t=tui_new()
s=tui_new_style()
t.row=4
t.col=10
t.height=15
t.width=100
t.title="test panel"

border=s.border
bg=0; fg=1
for ,bg==fg,
    bg,fg=[rand(7),rand(7)]
endfor
border["bg"]=bg.as_string
border["fg"]=fg.as_string
s.bg=bg.as_string
s.fg=fg.as_string
s.border=border
s.fill=true
s.hi_fg="2"
s.hi_bg="7"

tui_screen(1)
t.tui_box(s)

menu=tui_new()
menu.row,menu.col,menu.action = [4,10,"menu"]
menu.options=[
    "cow","pig","cat","dog",
    "sheep","horse","monkey","spider","hamster",
]
menu.prompt ="Please select an option:"
menu.cursor="[#b1][#6]*[##][#-]"

result=menu.tui_menu(s)

# example progress bar
pb=tui_new()
pb.row,pb.col,pb.action = [2,20,"progress"]
pb.width=80
s.fg="5"
for e=1 to 50
    pb.value=e.as_float
    pb.tui_progress(s)
    pause 50
endfor
keypress()

# reset bar
pb.value=0.0
pb.tui_progress(s)

# pager
s.bg,s.fg=["2","7"]
border["bg"]=s.bg
border["fg"]=s.fg
s.border=border
t.height,t.width,t.title=[30,118,"modal text box"]
t.tui_box(s)
t.content=$in "CHANGELOG"
t.tui_text_modal(s)

# cleanup
tui_screen(0)

println "menu result -> ",result
on result>0 do println "     option -> ",menu.options[result-1]
cursoron()

