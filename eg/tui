#!/usr/bin/za

seed(-1)

trap("int","cleanup()")

def cleanup()
    tui_screen(0)
    cursoron()
    exit
end

struct member
    forename    string
    surname     string
    age         int
    town        string
endstruct

var members[] any
append_to(ref members, member(.forename "Fred", .surname "Flintstone", .age 30, .town "Bedrock" ) )
append_to(ref members, member(.forename "Bugs", .surname "Bunny", .age 80, .town "Albuquerque" ) )
append_to(ref members, member(.forename "Stimpson J", .surname "Cat", .age 3, .town "Los Angeles" ) )

# switch to alt screen
tui_screen(1)

s=tui_new_style()
s.fill=true
border=s.border

i=tui_new()
rad=tui_new()
rad.Selected=[false,false,false,false].list_bool
pb=tui_new()
menu=tui_new()
tb=tui_new()
p=tui_new()

quit=false
while not quit

    # menu
    cursoroff()
    s.bg,s.fg       =["3","7"]
    s.hi_bg,s.hi_fg =["2","7"]
    border["bg"]="3"
    border["fg"]="7"
    s.border=border
    menu.Row,menu.Col,menu.Title = [2,2,"test options menu"]
    menu.Height,menu.Width=[12,32]
    menu.Options=[
        "input box","radio buttons","progress bar",
        "text box","pager","templates",
        "quit",
    ]
    menu.Prompt ="Please select an option:"
    menu.Cursor="[#b1][#6]*[##][#-]"

    menu.tui_box(s)
    menu=menu.tui_menu(s)
    on menu.Cancel do break

    case menu.Result
    is 1
        # input box
        i.Row=term_h()-4
        i.Col=10
        i.Content="blah"
        i.Prompt="[#b1][#6]type away... "
        i.Title="test input box"
        i.Border=true
        i.Height,i.Width=[1,80]
        s.bg,s.fg=["1","6"]
        border["bg"]=s.bg
        border["fg"]=s.fg
        s.border=border
        i=i.tui_input(s)
        ires=i.Result
    is 2
        # radio buttons
        rad.Row,rad.Col=[2,36]
        rad.Height,rad.Width=[6,64]
        rad.Prompt="Please select : "
        rad.Options=["Red","Green","Blue","White"]
        rad.Vertical=true
        rad.Multi=true
        rad.Sep=" "
        s.bg,s.fg=["0","5"]
        rad=rad.tui_radio(s)
        rad.tui_clear
        # rad.Selected [] contains end state
    is 3
        # example progress bar
        pb.Row,pb.Col = [20,20]
        pb.Width=80
        pb.Cursor="â–’"
        pb.Border=true
        s.bg,s.fg=[as_string(rand(5)),as_string(5+rand(2))]
        for e=1 to 100
            pb.Value=e.as_float/100
            pb=pb.tui_progress(s)
            pause 10
        endfor
        # reset bar
        pb=pb.tui_progress_reset
    is 4
        # text box
        tb.Row,tb.Col=[2,36]
        tb.Height,tb.Width,tb.Title=[15,40,"text display"]
        border["bg"]=s.bg
        border["fg"]=s.fg
        s.wrap=true
        s.bg,s.fg=["3","7"]
        tb.Content="Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua. Ut enim ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut aliquip ex ea commodo consequat. Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est laborum."
        tb.tui_box(s)
        tb.tui_text(s)
        keypress(0)
        tb.tui_clear
    is 5
        # pager
        p.Row,p.Col=[2,36]
        s.bg,s.fg=["2","7"]
        s.wrap=false
        border["bg"]=s.bg
        border["fg"]=s.fg
        s.border=border
        p.Height,p.Width,p.Title=[30,110,"pager box"]
        p.tui_box(s)
        p.Content=$in "CHANGELOG"
        p.tui_pager(s)
        p.tui_clear
    is 6
        # templates
        tb.Height,tb.Width,tb.Title=[4,40,"template display"]
        s.wrap=true
        s.bg,s.fg=["1","7"]
        border["bg"]=s.bg
        border["fg"]=s.fg
        tb.Content="Name     : {.surname}, {.forename}\nAge      : {.age}\nLocation : {.town}"
        foreach rec in members
        tb.Title=" Record #{=key_rec+1} "
            tb.Row,tb.Col=[2+6*key_rec,80]
            tb.Data=rec
            tb.tui_box(s)
            tb.tui_template(s)
        endfor
        keypress()
        foreach rec in members
            tb.Row,tb.Col=[2+6*key_rec,80]
            tb.tui_clear
        endfor
    is 7
        quit=true
    endcase

endwhile

# cleanup
tui_screen(0)
cursoron()
at term_h(),1


