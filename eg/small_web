#!/usr/bin/za

# minimal static web server below.

# change these to suit:
# HOME_PATH in web_serve_start() currently needs to be absolute path

define interface_ip(in)
    addrline =< ip addr show dev {in}
    fil=addrline.reg_filter("inet.*/")
    return addrline[fil[0][0]:fil[0][1]].reg_replace("inet ","").gsub("/","")
end

define routing_interface()
    i =< ip -o -4 route show to default
    return field(i,5)
end

HOST=routing_interface().interface_ip
PORT=61440
HOME_PATH="/home/daniel/www"

permit("shell",false)
permit("eval",false)

trapInt="cleanup(sid)"

define cleanup(s)
    web_serve_stop(s)
    log "Server [#{s}] Stopped.\n"
    exit 0
end

sid = web_serve_start(HOME_PATH,PORT,HOST)

log "Server [#{sid}] Started.\n"

web_serve_path(sid, "s",
    "^/$", "http://{HOST}:{PORT}/index.html"
)

web_serve_path(sid, "s",
    `/(.*\.(|html?|js|css|png|jpg|ico))$`, `http://{HOST}:{PORT}/$1`
)

web_serve_path(sid, "s",
    "^/(.*)/$", "http://{HOST}:{PORT}/$1/index.html"
)

while web_serve_up(sid)
    # in process tasks go here:
    pause 200
endwhile


