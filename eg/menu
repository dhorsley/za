#!/usr/bin/za

#
# example menu proc
#

ansi(true)

pane define "pop",term_h()/2,1,2,term_w(),"","round"

define popup(s)
    pane select "pop"
    pane redraw
    at 1,term_w()/2-len(s)/2
    print s
    keypress(0)
    pane select "global"
end

# simple menu, only accepts up to 9 options
define menu(title_prompt,opts)
    cls
    cursoroff()

    at 2,10
    print "[#b1]{title_prompt}[##]"

    foreach c in opts
        at 4+key_c,10
        print "[#b1][{=1+key_c}][##] {c}"
    ef

    maxchoice=49+len(opts)

    at 5+len(opts),10
    print "[#b2][q][##] Quit menu"

    n=-1
    while n==-1
        k=keypress()
        if k>=49 && k<maxchoice
            n=k-48
        ei
        on char(k)=="q" do break
    ew

    cursoron()
    return n

end

def opt_read()
    "READ".popup
end

def opt_write()
    "WRITE".popup
end

def opt_delete()
    "DELETE".popup
end

#########################################################

choice=0

while choice!=-1

    sub_choice=0
    choice=menu( "Main Menu", [ "First", "Second", "Third" ] )

    case choice
    is 1
        while sub_choice!=-1
            sub_choice=menu("First Menu",["Read","Write","Delete"])
            case sub_choice
            is 1
                opt_read()
            is 2
                opt_write()
            is 3
                opt_delete()
            ec
        ew
    is 2
        while sub_choice!=-1
            sub_choice=menu("Second Menu",[])
        ew
    ec

ew

println "\n\n\n"


