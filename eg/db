#!/usr/bin/za

define clean_name(n)
    # some kind of injection check
    on n == "" do return false,n
    on n ~ `\\` do return false,n
    n=n.replace("'","\'")
    return n ~i "^[a-z -\\']*$",n
end

# do a 'export ZA_DB_PASS="something"' ahead of this script!

set_env("ZA_DB_HOST","127.0.0.1")
set_env("ZA_DB_ENGINE","mysql")
set_env("ZA_DB_PORT","3306")
set_env("ZA_DB_USER","root")

h=db_init("mysql")

res=h.db_query("show tables","|")
println "\n[#1]table list[#-]\n{res}\n"


# obviously, you would need to verify and validate the input
# to avoid things like this: u="daniel' or 'a'='a" :

while
    prompt u "User to search for: " ; print
    ok,u=clean_name(u)
    on ok do break
endwhile

println "\n[#1]user list[#-]"

res=h.db_query("select Host,User,Password from user where user='{u}'","|")

struct s_u
    host    string
    user    string
    pass    string
endstruct

var user s_u

foreach l in res
    fields(l,"|")
    user=s_u(
        F[1],
        F[2],
        F[3]
    )
    println format("%+v",user) 
endfor
print

h.db_close


