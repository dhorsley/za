#!/usr/bin/za

module "modules/menu"

define ssh_menu(f)
    capture=|aws --region eu-west-1 --profile itsupport ec2 describe-instances --filters "Name=tag:cluster,Values={f}"
    instances=capture.out.json_query(
        ".Reservations[].Instances[] | {iid:.InstanceId, itype:.InstanceType, ami:.ImageId, iip:.PrivateIpAddress, eip:.PublicIpAddress}",
        true
    )
    foreach i in instances
        iary=iary.append("[#2]%20s [#3]%16s [#4]%20s[#-]".format(i["iid"],i["itype"],i["iip"]))
        iplist=iplist.append(i["iip"])
    endfor
    choice=menu("Select Server...", iary)
    on choice==-1 do return

    # start ssh
    println "\n"
    coproc(false)
    capture_shell(false)
    | ssh -o StrictHostKeyChecking=no ec2-user@{=iplist[choice-1]}

    # report errors, wait for keypress
    println last_out()
    println "\n[#4]Press a key to return to the main menu[#-]"
    keypress(0)

end
 

# main menu

quitting=false

while not quitting

    choice=menu(
        "Server Type", 
        [
            "Cognos - Test",
            "Cognos - Stage",
            "Cognos - Live"
        ]
    )

    when choice
      is -1
        quitting=true
      is 1
        ssh_menu("_BI.Cognos11Test_")
         break
      is 2
        ssh_menu("_BI.Cognos11Stage_")
         break
      is 3
        ssh_menu("_BI.Cognos11_")
        break
    endwhen

endwhile

# end
at 10,1
exit 0

