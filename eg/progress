#!/usr/bin/za

# hs=term_w()-20
hs=20
rep=1
input rep optarg 1

define bar(row,col,hsize,pc,colour)
    part_3q="▊"
    part_2q="▌"
    part_1q="▎"
    us = hsize / 100f   # 1% width of total
    d  = pc*us          # width of input percent
    r  = d-d.int        # remainder
    for e=0 to hsize-1
        at row,col+e
        c=" "
        on e<d.int do c="█"
        if e>=d.int and e<int(d+1)
            on r>=0.25*us do c=part_1q
            on r>=0.50*us do c=part_2q
            on r>=0.75*us do c=part_3q
        endif
        print "[#{colour}]{c}[#-]"
    endfor
end

cursoroff()
seed(epoch_time())

for i=0 to rep.int-1
    row=get_row()
    c=rand(7)
    for e = 0 to 100
        at row,1,"Progress : " ; bar(row,12,hs,e,c)
        msg=" {e} % "
        # at row,(term_w()-msg.len)/2,msg
        print msg
        pause 1
    endfor
    println
    hs=8+rand(16)
endfor

cursoron()
print


