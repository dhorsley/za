#!/usr/bin/za

doc : test program for json handling
doc :   this script will traverse json file content
doc :    and colourise the content by depth


# FUNCTIONS

define heading(s1,d)
    spacing=""
    on d>0 do spacing=" "*d*4
    println format("%s%29s[][#-]:",spacing,s1)
end

define value(s1,s2,d)
    spacing=""
    on d>0 do spacing=" "*d*4
    println format("%s%30s : %v",spacing,s1,s2)
end

define pp(k,i,d)

    # set colour depth
    print "[#"+string((d+1)%7)+"]"

    when
    has i.kind ~ `^map\[`
        heading(k,d)
        foreach si in i
            pp(key_si,si,d+1)
        endfor 
    has i.kind ~ `^\[map`
        heading(k,d)
        foreach si in i[0]
            pp(key_si,si,d+1)
        endfor
    has i.kind ~ `\[]`
        heading("[arr] "+k,d)
        on i.len==0 do break
        foreach si in i[0]
            pp(key_si,si,d+1)
        endfor
    has ! i.kind ~ `\[`
        value(k,i,d)
    endwhen

    # reset colour
    print "[#-]"

end


# MAIN

f= <- "json-test.json"
on f==nil do exit 1,"Error: could not read JSON file."

json=json_decode(f)

# deep iteration over pairs
foreach i in json["Reservations"][0]["Instances"][0]
    pp(key_i,i,0)
endfor


