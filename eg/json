#!/usr/bin/za

doc : test program for json handling
doc :   this script will traverse json file content
doc :    and colourise the content by depth


# FUNCTIONS

define heading(s1,d)
    spacing=""
    on d>0 do spacing=" "*d*4
    println format("%s%29s[][#-]:",spacing,s1)
end

define value(s1,s2,d)
    spacing=""
    on d>0 do spacing=" "*d*4
    println format("%s%30s : %v",spacing,s1,s2)
end

define pp(k,i,d)

    # set colour depth
    print "[#"+string((d+1)%7)+"]"

    if i.kind ~ `^map\[`
        # map
        heading(k,d)
        foreach si in i
            pp(key_si,si,d+1)
        endfor 
    endif

    if i.kind ~ `^\[map`
        # array of map
        heading(k,d)
        foreach si in i[0]
            pp(key_si,si,d+1)
        endfor
    endif

    if i.kind ~ `\[\]`
        # array
        heading("[arr] "+k,d)
        if len(i)>0
            foreach si in i[0]
                pp(key_si,si,d+1)
            endfor
        endif
    endif

    if ! i.kind ~ `\[`
        # value
        value(k,i,d)
    endif

    # reset colour
    print "[#-]"

end


# MAIN

f=read_file("json-test.json")
if f==nil
    exit 1,"Error: could not read JSON file."
endif

json=json_decode(f)

# drill down to right level
instances=json["Reservations"][0]["Instances"]

# deep iteration over pairs
foreach i in instances[0]
    pp(key_i,i,0)
endfor

