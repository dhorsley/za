#!/usr/bin/za

doc `
    detect unbalanced quotes in input file.
     - does not deal with utf-8 yet, comments skipped (# or //).
`

input filename param 1 is "filename"

def rep(e,m)
    cursorx(offset+e)
    # print m,"[#-] @ ",row+1,",",e+1
    print m
    # on not (osq or odq) do println
    @extra=true
end

offset=9
osq=false
odq=false
obt=false

fh=fopen(filename,"r")
on fh==nil do exit 66,"File {filename} cannot be read."

struct pos
    sl   int
    sc   int
    fl   int
    fc   int
endstruct

var ssp,dsp pos

p=0
row=0
col=0
lp=0

interpol(false)
while
    l=fh.fread("\n")
    if l.len==0 
        row+=1
        on !fh.feof do continue
        break
    endif
    print fgrgb(30,60,60)+format("%05d : ",row+1)+addansi("[#-]")
    l.literal
    println
    extra=false
    for e=0 to l.len-1
        if not (odq or osq or obt) and l[e:] ~ "(^#|//)"
            # println "skipping comment: ",l[e:]
            break
        endif
        case l[e]
        is `"`
            if not (osq or obt)
                odq=!odq
                if odq
                    rep(e,"[#4]^")
                    dsp.sl,dsp.sc=[row+1,e+1]
                    dsp.fl,dsp.fc=[0,0]
                else
                    rep(e,"[#2]$")
                    dsp.fl,dsp.fc=[row+1,e+1]
                    dsp.sl,dsp.sc=[0,0]
                endif
            else
                # rep(e,"[#6]skipping double")
            endif
        is `'`
            if not (odq or obt)
                osq=!osq
                if osq
                    rep(e,"[#6]^")
                    ssp.sl,ssp.sc=[row+1,e+1]
                    ssp.fl,ssp.fc=[0,0]
                else
                    rep(e,"[#2]$")
                    ssp.fl,ssp.fc=[row+1,e+1]
                    ssp.sl,ssp.sc=[0,0]
                endif
            else
                # rep(e,"[#6]skipping single")
            endif
        is "`"
            on not (osq or odq) do obt=!obt
        endcase
    endfor
    row++
    on extra do println
    on fh.feof do break
endwhile
p=fh.ftell
fh.fclose
interpol(true)

println
println "file name   : ",filename
println "file length : ",p
println
on ssp.sl!=0 and ssp.fl==0 do println "Unbalanced single quotes begin @ line {=ssp.sl} col {=ssp.sc}"
on dsp.sl!=0 and dsp.fl==0 do println "Unbalanced double quotes begin @ line {=dsp.sl} col {=dsp.sc}"

