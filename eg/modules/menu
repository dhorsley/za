#!/usr/bin/za

define menu(p,opts)

    cls
    cursoroff()

    at 2,10
    print "[#b1]{p}[##]"

    sel=0
    ol=opts.len

    # determine shortcut keys per option
    case
    has ol<10
        key_range = "1".asc .. "1".asc+ol-1
        break
    has ol<30
        key_range = ( "1".asc .. "9".asc ) +  ( "a".asc .. "a".asc+ol-10 )
        break
    has ol>=30
        key_range = nil
    ec

    # display menu
    foreach p in opts
        at 4+key_p,12
        short_code=" "
        on key_range!=nil do short_code=key_range[key_p].char
        print "[#b1][{=short_code}][##] {p}"
    endfor

    maxchoice=49+len(opts)

    at 5+opts.len,10
    print "[#b2][q][##] Quit menu"

    # input loop
    finished=false
    n=-1
    while n==-1 and not finished

        at 4+sel,10 ; print "[#invert]-[#-]"
        k=keypress()
        at 4+sel,10 ; print " "

        if k>=49 && k<maxchoice
            n=k-48
        endif

        on char(k)=="q" do break

        case k
        is 11
            sel=sel-btoi(sel>0)
        is 10
            sel=sel+btoi(sel<opts.len-1)
        is 13
            n=sel+1
            finished=true
        ec

    endwhile

    cursoron()
    return n

end

