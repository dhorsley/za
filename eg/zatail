#!/usr/bin/za

doc `
    rough attempt at a logtail equivalent. based on the perl one.
`

stdin="/dev/stdin"

testmode=false
logfile=stdin

input logfile optarg 1 is "filename"
input offfile optarg 2 is "offset filename"
input tm_inp optarg 3 is "test mode"
on tm_inp!="" do testmode=as_bool(tm_inp)

if argc()==0
    println "Usage:"
    println "   zatail [file_name] [offset_file_name] [test_mode_bool]"
    exit 126
endif

on file_mode(logfile) == -1 do exit 66,"File {logfile} cannot be read."
on offfile=="" and logfile!=stdin do offfile=logfile+".offset"

fh=fopen(logfile,"r")
on fh==nil do exit 66,"File {logfile} cannot be read."

inode=0
ino=0
offset=0

oh=nil
on offfile.is_file do oh=fopen(offfile,"r")

if oh!=nil

    if oh.ftell!=nil
        inode=fread(oh,"\n").as_int
        offset=fread(oh,"\n").as_int
        #println "r-INO : ",inode
        #println "r-OFF : ",offset
    endif
    fclose(oh)

    size=file_size(logfile)
    on size==-1 do exit 65,"Cannot get {logfile} file size."
    ino=stat(logfile).sys.Ino.as_int

    if inode==ino
        on offset==size do exit 0  
        if offset>size
            offset = 0
            println "[#2]***************"
            println "*** WARNING ***: Log file {logfile} is smaller than last time checked!"
            println "*************** This could indicate tampering.[#-]"
        endif
    endif

    on inode!=ino or offset>size do offset=0

    fh.fseek(offset,0)

endif

interpol(false)
while not feof(fh)
    println fread(fh,"\n")
endwhile
interpol(true)

on logfile!=stdin do size=fh.ftell
fclose(fh)

if not testmode and offfile!=""
    oh=fopen(offfile,"w")
    on oh==nil do exit 73,"File {offfile} cannot be created. Check your permissions."
    #println "w-INO : ",ino
    #println "w-OFF : ",size
    fwrite(oh,"{=ino}\n{=size}\n")
    fclose(oh)
endif

exit

