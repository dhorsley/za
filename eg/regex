#!/usr/bin/za

define runFilter(s,v)
    f=filter(s,v,-1)
    println "filter [{v}] done."
    return format("%s %d", v, len(f))
enddef

define runSubs(subs,seq)
    foreach v in subs
        println `Sub for {v[0]},{v[1]}`
        seq=replace(seq,v[0],v[1])
    endfor
    return len(seq)
enddef

define perf_regex_dna()

    variants = [
          "agggtaaa|tttaccct",
          "[cgt]gggtaaa|tttaccc[acg]",
          "a[act]ggtaaa|tttacc[agt]t",
          "ag[act]gtaaa|tttac[agt]ct",
          "agg[act]taaa|ttta[agt]cct",
          "aggg[acg]aaa|ttt[cgt]ccct",
          "agggt[cgt]aa|tt[acg]accct",
          "agggta[cgt]a|t[acg]taccct",
          "agggtaa[cgt]|[acg]ttaccct"
    ]

    subs=append( subs, [ `tHa[Nt]`, `<4>`            ] )
    subs=append( subs, [ `aND|caN|Ha[DS]|WaS`, `<3>` ] )
    subs=append( subs, [ `a[NSt]|BY`, `<2>`          ] )
    subs=append( subs, [ `<[^>]*>`, `|`              ] )
    subs=append( subs, [ `\|[^|][^|]*\|`, `-`        ] )

    seq = read_file("/dev/stdin")
    l1 = len(seq)

    seq = replace(seq, ">.*\n|\n", "")
    l2 = len(seq)

    foreach v in variants
        async ahandles runFilter(seq,v) "filter_{key_v}"
    endfor

    async ahandles runSubs(subs,seq) "subtot"

    # wait for completion
    res=await(ahandles,true)

    # push to screen in same order they were sent for processing
    for e=0 to len(res)-1
        on key("res","filter_{e}") do println format("%s",res["filter_{e}"])
    endfor

    println
    println l1
    println l2
    println res["subtot"]

enddef

perf_regex_dna()

