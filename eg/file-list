#!/usr/bin/za

# read a file line-by-line
# test filter and map operators on it

var words []string
var n int

testfile="/usr/share/dict/american-english"

when release_id()
is "fedora"
    testfile="/usr/share/dict/words"
is "ubuntu"
    pkg="wamerican"
    # example quiet install for missing package:
    on !is_installed(pkg) do install(pkg,true)
endwhen

if is_file(testfile)
    q=fopen(testfile,"r")
    while !feof(q)
        words[n++]=fread(q,"\n")
    endwhile
    fclose(q)
else
    exit 1,"File not found. ({testfile})"
endif

println words.len

println "starting with z and len > 4:"
w1 = words ?> "# ~ `^z` and #.len>4"
println w1,"\n"

println "mapped to upper then drop apostrophes and lose front and back char and get unique words:"
println ( w1 -> "upper(#)" -> `#.tr("d","'")` -> "#[1:#.len-1]" ).uniq ,"\n"


