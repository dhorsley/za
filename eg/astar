#!/usr/bin/za

module "./astar.mod" as a
use +a

max_block = 550

def set(c,r,t)
    @g[r*mw+c]=t
end

def get(c,r)
    return g[r*mw+c]
end

def fill_path(grid,sx,sy,ex,ey)
    ng,status,steps=find_path(grid,mw,mh)

    case status
    is waytypes.InvalidInput
        exit 1,"\nInvalid Input"
    is waytypes.NoPath
        exit 2,"\nNo Path Found"
    endcase
    return ng,steps
end

def show_grid(g)
    colarr="241"
    colpos=0
    for p=0 to mh*mw-1
        r,c=[as_int(p/mw),p%mw]
        case g[r*mw+c]
        is ptype.block
            at r+1,c+1,"░"
        is ptype.start
            at r+1,c+1,"[#invert][#3]S[#-]"
        is ptype.target
            at r+1,c+1,"[#invert][#6]E[#-]"
        is ptype.path
            colour=colarr[colpos++%3]
            at r+1,c+1,"[#{colour}]•[#-]"
        endcase
    endfor
end

def randx()
    return rand(mw)-1
end

def randy()
    return rand(mh-1)-1
end

seed(-1)

# setup grid
mh=term_h()
mw=term_w()
on mh==-1 or mw==-1 do mh,mw=[25,80]

var g [mh*mw] int
for e=0 to mh*mw-1
    g[e]=ptype.empty
endfor


# add start
px,py=[randx(),randy()]
set(px,py,ptype.start)


# add target
tx,ty=[px,py]
while px==tx and py==ty
    tx,ty=[randx(),randy()]
endwhile
set(tx,ty,ptype.target)


# add blocks
for e=0 to max_block-1
    for ,,
        rx,ry=[randx(),randy()]
        if get(rx,ry)==ptype.empty
            set(rx,ry,ptype.block)
            break
        endif
    endfor
endfor


# show starting grid
cls
show_grid(g)
at mh,1, "continue? mh {mh} mw {mw} :: mh*mw {=mh*mw}"
keypress()

# calc nav
t1=epoch_nano_time()
ng,steps=fill_path(g,px,py,tx,ty)
t2=time_diff(epoch_nano_time(),t1)


# show end grid
cls
show_grid(ng)

# cleanup
at mh,1, "continue? (calc time : %.3fs) (steps : %d)".format(t2/1_000_000,steps)
keypress()
cls
#println g ?> `# != 0`



